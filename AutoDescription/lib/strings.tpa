/* --------------------------------------------------------------- *
 * Récupération des textes déjà existant des types de compétences. *
 * --------------------------------------------------------------- */
COPY_EXISTING ~weapprof.2da~  ~override~
	COUNT_2DA_ROWS ~3~ ~rows~
	FOR ( row = 1 ; row < rows ; row = row + 1 ) BEGIN
		READ_2DA_ENTRY row 2 22 strref
		READ_2DA_ENTRY row 1 22 offset

		PATCH_IF NOT ~%strref%~ STRING_EQUAL ~4294967296~ BEGIN
			GET_STRREF  strref text
			SPRINT $tra_proficiencies(~%offset%~) ~%text%~
		END
	END
    BUT_ONLY_IF_IT_CHANGES

/* -------------------------------------------------------- *
 * Récupération des textes déjà existant des noms des kits. *
 * -------------------------------------------------------- */
COPY_EXISTING ~kitlist.2da~  ~override~
	COUNT_2DA_ROWS ~3~ ~rows~
	FOR (row = 2 ; row < rows ; row = row + 1) BEGIN
		READ_2DA_ENTRY row 1 8 name
		READ_2DA_ENTRY row 3 8 strref
		PATCH_IF is_ee BEGIN
			READ_2DA_ENTRY row 9 8 kitIds
		END

		PATCH_IF NOT ~%strref%~ STRING_EQUAL ~4294967296~ BEGIN
			GET_STRREF  strref text
			SPRINT $translations(~%name%~) ~%text%~
			PATCH_IF is_ee BEGIN
				SPRINT $kits(~%kitIds%~) ~%text%~
			END
		END
	END
    BUT_ONLY_IF_IT_CHANGES

/* ---------------------------------------------------------------- *
 * Récupération des textes déjà existant des infobulles des objets. *
 * ---------------------------------------------------------------- */
COPY_EXISTING ~tooltip.2da~ ~override~
	COUNT_2DA_ROWS ~4~ ~rows~
	FOR ( row = 1 ; row < rows ; row = row + 1 ) BEGIN
		READ_2DA_ENTRY row 0 4 itemId
		READ_2DA_ENTRY row 1 4 strref0
		READ_2DA_ENTRY row 2 4 strref1
		READ_2DA_ENTRY row 3 4 strref2

		TO_LOWER itemId

		PATCH_DEFINE_ARRAY $tra_tooltips(~%itemId%~) BEGIN ~%strref0%~ ~%strref1%~ ~%strref2%~ END
	END
    BUT_ONLY_IF_IT_CHANGES


DEFINE_ACTION_FUNCTION ~ucfirst~ STR_VAR value = ~~ RET string BEGIN
	OUTER_SNPRINT 1 first ~%value%~
	ACTION_TO_UPPER first

	OUTER_PATCH_SAVE string ~%value%~ BEGIN
		REPLACE_TEXTUALLY EVALUATE_REGEXP ~^.?~ ~%first%~
	END
END

/* ----------------------------------------------------------------------------------- *
 * Des chaînes récupérées directement depuis leur référence dans le fichier dialog.tlk *
 * Possible aussi pour les multi-classes, mais demande un traitement différent.        *
 * ----------------------------------------------------------------------------------- */
ACTION_DEFINE_ASSOCIATIVE_ARRAY ~string_from_strref~ BEGIN
	// Races
	~DWARF~    => 7182
	~HUMAN~    => 7193
	~ELF~      => 7194
	~HALFLING~ => 7195
	~GNOME~    => 7196
	~HALF_ELF~ => 7197
	~HALFORC~  => 53186
	// Classes
	~FIGHTER~  => 10086
	~RANGER~   => 7200
	~PALADIN~  => 7217
	~CLERIC~   => 7204
	~DRUID~    => 7210
	~MAGE~     => 18039
	~THIEF~    => 7202
	~BARD~     => 7206
	~MONK~     => 45851
	~RANGER~   => 7200
END

ACTION_PHP_EACH string_from_strref AS name => strref BEGIN
	ACTION_GET_STRREF strref value
	LAF ~ucfirst~ STR_VAR value RET string END
	OUTER_SPRINT $translations(~%name%~) ~%string%~
END

/* -------------------------------------- *
 * Chaines utilisées dans le opcode 232 . *
 * -------------------------------------- */
OUTER_SPRINT $op232Target(~0~) @102472 // ~le porteur~
OUTER_SPRINT $op232Target(~1~) @102536 // ~l'attaquant~
OUTER_SPRINT $op232Target(~2~) @102535 // ~l'adversaire le plus proche~
OUTER_SPRINT $op232Target(~3~) @102653 // ~la créature la plus proche~

OUTER_SPRINT $op232Condition(~0~) @102660 // ~lorsque le porteur est touché~
OUTER_SPRINT $op232Condition(~1~) @102661 // ~lorsqu'un adversaire entre dans le champ de vision~
OUTER_SPRINT $op232Condition(~2~) @102662 // ~lorsque les points de vie du porteur passent sous les 50%~
OUTER_SPRINT $op232Condition(~3~) @102663 // ~lorsque les points de vie du porteur passent sous les 25%~
OUTER_SPRINT $op232Condition(~4~) @102664 // ~lorsque les points de vie du porteur passent sous les 10%~
OUTER_SPRINT $op232Condition(~5~) @102665 // ~lorsque le porteur est sans défense~
OUTER_SPRINT $op232Condition(~6~) @102666 // ~lorsque le porteur est empoisonné~
OUTER_SPRINT $op232Condition(~7~) @102667 // ~à chaque round où le porteur est attaqué~
OUTER_SPRINT $op232Condition(~8~) @102668 // ~lorsque la cible se trouve à moins de 2,5 mètres~
OUTER_SPRINT $op232Condition(~9~) @102669 // ~lorsque la cible se trouve à moins de 6 mètres~
OUTER_SPRINT $op232Condition(~10~) @102670 // ~à chaque round~
OUTER_SPRINT $op232Condition(~11~) @102671 // ~lorsque le porteur subit des dégâts~

/* ----------------------------------------------------------------- *
 * D'autres chaînes utilisées dans la partie "(Non) Utilisable par". *
 * Je n'ai pas trouvé le moyen de récupérer les textes               *
 * déjà existant.                                                    *
 * ----------------------------------------------------------------- */
// Pas trouvé comment récupérer les chaînes déjà existantes des ces éléments
OUTER_SPRINT $translations(~CHAOTIC~) @101000
OUTER_SPRINT $translations(~EVIL~) @101001
OUTER_SPRINT $translations(~GOOD~) @101002
OUTER_SPRINT $translations(~NEUTRAL~) @101003
OUTER_SPRINT $translations(~LOYAL~) @101004
OUTER_SPRINT $translations(~CLERIC_MAGE~) @101008
OUTER_SPRINT $translations(~CLERIC_THIEF~) @101009
OUTER_SPRINT $translations(~CLERIC_RANGER~) @101010
OUTER_SPRINT $translations(~FIGHTER~) @101011
OUTER_SPRINT $translations(~FIGHTER_DRUID~) @101012
OUTER_SPRINT $translations(~FIGHTER_MAGE~) @101013
OUTER_SPRINT $translations(~FIGHTER_CLERIC~) @101014
OUTER_SPRINT $translations(~FIGHTER_MAGE_CLERIC~) @101015
OUTER_SPRINT $translations(~FIGHTER_MAGE_THIEF~) @101016
OUTER_SPRINT $translations(~FIGHTER_THIEF~) @101017
OUTER_SPRINT $translations(~MAGE_THIEF~) @101019

OUTER_SPRINT crlf  @10000
OUTER_SPRINT colon @10001   // ~ : ~
OUTER_SPRINT meter @100005  // ~mètre~
OUTER_SPRINT meters @100006 // ~mètres~