BACKUP ~AutoDescription/backup~
AUTHOR ~Selphira (https://www.baldursgateworld.fr/lacouronne/members/selphira.html)~

VERSION ~0.8.0 alpha~

ALWAYS
    OUTER_SPRINT BGFORGE_LIB_DIR "%MOD_FOLDER%/lib/bgforge"
    INCLUDE ~%BGFORGE_LIB_DIR%/main.tpa~
    INCLUDE ~%MOD_FOLDER%/lib/always.tpa~
END

LANGUAGE ~Francais~ ~french~
         ~AutoDescription/tra/french/setup.tra~
         ~AutoDescription/tra/french/description.tra~

BEGIN @10 DESIGNATED 10

DEFINE_PATCH_MACRO ~description_ammo~ BEGIN
	PATCH_PRINT "----------- MUNITION : %SOURCE_FILE% -----------"
END

DEFINE_PATCH_MACRO ~description_potion~ BEGIN
	PATCH_PRINT "----------- POTION : %SOURCE_FILE% -----------"
	READ_STRREF ITM_identified_desc description

	// TODO: Supprimer le texte non roleplay si présent (pas toujours de "PARAMETRES" ici..)

	LPF ~usability~ RET description END
END

DEFINE_PATCH_MACRO ~description~ BEGIN
	LPF ~equipped_abilities~ RET description END
	LPF ~extended_abilities~ RET description END

	PATCH_IF isWeapon == 1 BEGIN
		LPF ~proficiency~   RET description END
	END

	LPF ~appendLine~         RET description END
	LPF ~weight~             RET description END
	LPF ~needs~              RET description END
	LPF ~usability~          RET description END

	INNER_PATCH_SAVE description ~%description%~ BEGIN
		// Supprime les lignes vides consécutives
		REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~\(%crlf%%crlf%%crlf%\)+~ ~%crlf%%crlf%~
	END
END

DEFINE_PATCH_MACRO ~log~ BEGIN
	LOCAL_SPRINT filename ~%SOURCE_RES%~
	INNER_ACTION BEGIN
		SILENT
		<<<<<<<< .../AutoDescription/inlined/%filename%-before.log
		>>>>>>>>
		<<<<<<<< .../AutoDescription/inlined/%filename%-after.log
		>>>>>>>>
		COPY ~.../AutoDescription/inlined/%filename%-before.log~ ~AutoDescription/log~
		COPY ~.../AutoDescription/inlined/%filename%-after.log~ ~AutoDescription/log~
		APPEND_OUTER ~AutoDescription/log/%filename%-before.log~ ~// "%oldDescription%"~
		APPEND_OUTER ~AutoDescription/log/%filename%-after.log~ ~// "%description%"~
		VERBOSE
	END
END


OUTER_SET total = 0
OUTER_SET totalPotion = 0
OUTER_SET totalAmmo = 0
OUTER_SET totalWithoutDescription = 0
OUTER_SET totalFailed = 0
OUTER_SET totalSuccessful = 0
OUTER_SET totalIgnored = 0

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // Evite un crash sur les objets vides
		READ_SHORT  ITM_type itemType
		READ_STRREF ITM_identified_desc description
		READ_LONG   ITM_identified_desc strref
        READ_SHORT  ITM_flags           flags
        READ_SHORT  ITM_enchantment     enchantment

		SET total += 1
        SET isWeapon = 0

        PATCH_SILENT

		PATCH_IF ~%description%~ STRING_EQUAL ~~ OR strref == ~-1~ OR strref == 0 BEGIN
			SET totalWithoutDescription += 1
		END
		ELSE BEGIN

	        SPRINT oldDescription "%description%"

			LPF ~removeTechnicalDescription~ STR_VAR description RET description END

			// Si l'objet ne peut être déplacé, il n'est pas accessible au joueur
			PATCH_IF (flags BAND BIT2) == 0 BEGIN
				SET totalIgnored += 1
			END
			ELSE BEGIN
				PATCH_TRY
				    PATCH_IF itemType > 0xe AND itemType < 0x1f BEGIN // Les armes
				        SET isWeapon = 1
						LPM ~description~
						SET totalSuccessful += 1
						LPM ~log~
				    END
				    ELSE BEGIN
						PATCH_MATCH itemType
						WITH
							// Description de l'équipement
							ITM_TYPE_armor ITM_TYPE_shield_not_in_iwd ITM_TYPE_necklace ITM_TYPE_amulet ITM_TYPE_belt ITM_TYPE_girdle ITM_TYPE_boots ITM_TYPE_bracers ITM_TYPE_gauntlets ITM_TYPE_helm ITM_TYPE_ring ITM_TYPE_cloak
							BEGIN
								LPM ~description~
								SET totalSuccessful += 1
								LPM ~log~
							END
							// Description des consommables
							ITM_TYPE_potion ITM_TYPE_wand ITM_TYPE_scroll
							BEGIN
								// LPM ~description_potion~
								SET totalPotion += 1
								SET totalIgnored += 1
							END
							// Description des munitions
							ITM_TYPE_arrows ITM_TYPE_bullets ITM_TYPE_bolts
							BEGIN
								// LPM ~description_ammo~
								SET totalAmmo += 1
								SET totalIgnored += 1
							END
							DEFAULT
								SET totalIgnored += 1
						END
				    END
				WITH DEFAULT SET totalFailed += 1 END
			END
		END
	END
BUT_ONLY_IF_IT_CHANGES

PRINT "Total : %total%"
PRINT "Sans description : %totalWithoutDescription%"
PRINT "Ignores : %totalIgnored%"
PRINT "Reussis : %totalSuccessful%"
PRINT "Echoues (Objectif 0): %totalFailed%"
PRINT "Potions (TODO): %totalPotion%"
PRINT "Munitions (TODO) : %totalAmmo%"