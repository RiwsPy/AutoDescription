BACKUP ~AutoDescription/backup~
AUTHOR ~Selphira (https://www.baldursgateworld.fr/lacouronne/members/selphira.html)~

VERSION ~0.9.14a~

ALWAYS
	OUTER_PATCH ~~ BEGIN
		PATCH_INCLUDE ~%MOD_FOLDER%/settings-default.ini~
	END

	ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/settings.ini~ BEGIN
		OUTER_PATCH ~~ BEGIN
			PATCH_INCLUDE ~%MOD_FOLDER%/settings.ini~
		END
	END

	OUTER_SET is_ee = (GAME_IS ~bgee bg2ee eet~) ? 1 : 0

    ACTION_DEFINE_ARRAY arrayNoConvert BEGIN ~setup~ END
    ACTION_DEFINE_ARRAY arrayReload BEGIN ~description~ END

    LAF HANDLE_CHARSETS
        INT_VAR
            infer_charsets = 1
        STR_VAR
            default_language = ~french~
            tra_path = EVAL ~%MOD_FOLDER%/tra~
            out_path = EVAL ~weidu_external/Lang/%MOD_FOLDER%~
            noconvert_array = arrayNoConvert
            reload_array = arrayReload
    END

	OUTER_SPRINT log_filename ~all~
    OUTER_SPRINT BGFORGE_LIB_DIR "%MOD_FOLDER%/lib/bgforge"
    INCLUDE ~%BGFORGE_LIB_DIR%/main.tpa~
    INCLUDE ~%MOD_FOLDER%/lib/always.tpa~

	DEFINE_ACTION_MACRO ~init_statistics~ BEGIN
		OUTER_SET total = 0
		OUTER_SET totalPotion = 0
		OUTER_SET totalAmmo = 0
		OUTER_SET totalWithoutDescription = 0
		OUTER_SET totalFailed = 0
		OUTER_SET totalSuccessful = 0
		OUTER_SET totalIgnored = 0
		OUTER_SET opcodeTodo = 0
	END

	DEFINE_ACTION_MACRO ~show_statistics~ BEGIN
		ACTION_PHP_EACH ignored_opcodes AS opcode => value BEGIN
			ACTION_IF value == 1 BEGIN
				OUTER_SET opcodeTodo += 1
			END
		END

		PRINT "Total : %total%"
		PRINT "Sans description : %totalWithoutDescription%"
		PRINT "Ignores : %totalIgnored%"
		PRINT "Reussis : %totalSuccessful%"
		PRINT "Echoues : %totalFailed%"
		PRINT "Potions (TODO): %totalPotion%"
		PRINT "Munitions (TODO) : %totalAmmo%"
		PRINT "Opcodes a coder : %opcodeTodo%"
	END
END

LANGUAGE ~Francais~ ~french~
         ~%MOD_FOLDER%/tra/french/setup.tra~
         ~%MOD_FOLDER%/tra/french/description.tra~

BEGIN @10 DESIGNATED 10

LAM ~init_log_file~
LAM ~init_statistics~
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	LPM ~update_description~
BUT_ONLY_IF_IT_CHANGES
LAM ~show_statistics~

BEGIN @20 DESIGNATED 20

PRINT @201
ACTION_READLN directory
OUTER_WHILE NOT(DIRECTORY_EXISTS ~%directory%~) BEGIN
	ACTION_IF IS_AN_INT ~%directory%~ AND ~%directory%~ == 0 BEGIN
		ABORT ~~
	END
	PRINT @202
	ACTION_READLN directory
END
OUTER_SPRINT log_filename ~%directory%~
ACTION_DEFINE_ARRAY ~itemFiles~ BEGIN END
LAF getItemsFromOtherMod STR_VAR directory = EVAL ~%directory%~ RET_ARRAY itemFiles END
LAM ~init_log_file~
LAM ~init_statistics~
ACTION_PHP_EACH itemFiles AS itemFile => value BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%itemFile%~ BEGIN
		COPY_EXISTING ~%itemFile%~ ~override~
			LPM ~update_description~
		BUT_ONLY_IF_IT_CHANGES
	END
END
LAM ~show_statistics~